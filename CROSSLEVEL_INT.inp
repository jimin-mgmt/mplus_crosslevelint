USEVARIABLES ARE
L2CTRL !Level2 Control variable
L1CTRL1 !Level1 Control variables
L1CTRL2
L1CTRL3
X Y1 Y2 W; !IV, DV, Moderator

MISSING = all(999);

WITHIN=
L1CTRL1
L1CTRL2
L1CTRL3;

BETWEEN=
L2CTRL
W;

CLUSTER=TEAM; !Cluster variable for L2

DEFINE:
X=(X1+X2+X3)/3;
Y1 =(Y1_1+Y1_2+Y1_3+Y1_4+Y1_5)/5;
Y2= (Y2_1+Y2_2+Y2_3)/3;
W=(W1+W2+W3+W4+W5)/5;

CENTER L1CTRL1 L1CTRL2 L1CTRL3 (groupmean); !Only for Level1 variables
CENTER L2CTRL W X Y1 Y2 (grandmean); !For variables to be analyzed at Level2

ANALYSIS:
TYPE=TWOLEVEL RANDOM;
ESTIMATOR=ML;

MODEL:
%WITHIN%
X Y1 Y2;

S1 | Y1 on X L1CTRL1 L1CTRL2 L1CTRL3; !create random slopes for cross-level interactions
S2 | Y2 on X L1CTRL1 L1CTRL2 L1CTRL3; !control variables included due to significant difference in effects of L1 control variables across clusters

X on L1CTRL1 L1CTRL2 L1CTRL3;

X with Y1 Y2;
Y1 with Y2;

%BETWEEN%
S1 S2 X Y1 Y2 W;

[S1](a1);
[S2](a2);

Y1 on W L2CTRL;
Y2 on W L2CTRL;

S1 on W (b1)
L2CTRL;

S2 on W (b2)
L2CTRL;

S1 with S2 W;
S2 with W;

MODEL CONSTRAINT:
NEW(MODLOW1 MODHIGH1 MODLOW2 MODHIGH2);
!SIMPLE SLOPE TEST FOR Y1 on X*W and Y2 on X*W at -1SD and +1SD for W
MODLOW1=a1+b1*(-0.82);
MODHIGH1=a1+b1*(0.82);
MODLOW2=a2+b2*(-0.82);
MODHIGH2=a2+b2*(0.82);

OUTPUT:
SAMPSTAT
CINTERVAL;
